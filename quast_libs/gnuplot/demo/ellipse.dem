#
# Ethan A Merritt - Feb 2007
# This demo requires support for 'set object ellipse'
#
load "gen-random.inc"

unset key
set xrange [-8:8]
set yrange [-8:8]
set size ratio 1.0
set zeroaxis
set border 0
set xtics axis
set ytics axis
set tics scale 0.5
set format xy ""

set title 'Example of `set object ellipse`'

set object 1 ellipse center 1.5,1.0 size 2.,4. angle 20. front fillstyle empty border -1
set obj 2 ellipse center 1.5, 1  size 6, 12  angle 20 front fs empty bo 3

A = pi/9.
plot $random using (1.5 + $1*cos(A)-2.*$2*sin(A)):(1.0 + $1*sin(A)+2.*$2*cos(A)) with dots

pause -1 "Hit return to continue"

set title 'Example of range-limited axes and tics'

set tics out scale 0.5
set xtics 1.0 border rangelimited nomirror
set ytics 1.0 border rangelimited nomirror
set format xy "%.0f"
set border 3

replot

pause -1 "Hit return to continue"

