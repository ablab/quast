#
# $Id: world.dem,v 1.10.2.1 2014/11/08 04:52:24 sfeam Exp $
#
#
set title "Gnuplot Correspondences\ngeographic coordinate system"
unset key
set xrange [-180:180]
set yrange [-90:90]
set yzeroaxis
set xtics geographic
set ytics geographic
set format x "%D %E"
set format y "%D %N"
#
# plot world map and correspondent locations as a +
plot 'world.dat' with lines lc rgb "blue" , 'world.cor' with points lt 1 pt 2
pause -1 "Hit return to continue"
#
# plot a '3D version using spherical coordinate system' of the world.
reset
unset key
set border
set yzeroaxis
set xtics
set ytics
set angles degrees
set title "3D version using spherical coordinate system"
set ticslevel 0
set view 70,40,0.8,1.2
set mapping spherical
set parametric
set samples 32
set isosamples 9
set urange [-90:90]
set vrange [0:360]
splot cos(u)*cos(v),cos(u)*sin(v),sin(u) with lines lc rgb "cyan" ,\
'world.dat' with lines lc rgb "blue" , 'world.cor' with points lt 1 pt 2
pause -1 "Hit return to continue"

# HBB 20000715: new demo:
# same plot, but with hidden3d active, plus axes through the
# poles:
set title "3D solid version with hidden line removal"
set hidden3d offset 0
set arrow from 0,0,-1.2 to 0,0,1.2 lc rgb "cyan" lw 2
set arrow from -1.2, 0, 0 to 1.2, 0, 0 nohead lc rgb "cyan" lw 1 
set arrow from 0, -1.2, 0 to 0, 1.2, 0 nohead lc rgb "cyan" lw 1
splot cos(u)*cos(v),-cos(u)*sin(v),sin(u) with lines lc rgb "cyan" ,\
'world.dat' u 1:2:(1.001) with lines lc rgb "blue" , 'world.cor' with points lt 1 pt 2
pause -1 "Hit return to continue"

unset arrow

#
# plot a '3D version using cylindrical coordinate system' of the world.
set title "3D version using cylindrical coordinate system"
set ticslevel 0.0
set view 70,40,0.8,1.2
set mapping cylindrical
set parametric
set samples 32
set isosamples 13
set urange [-180:180]
set vrange [-90:90]
splot cos(u),sin(u),v with lines lc rgb "cyan" ,\
'world.dat' with lines lc rgb "blue" lw 2, 'world.cor' with points lt 1 pt 2
pause -1 "Hit return to continue"
reset
